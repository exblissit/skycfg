# Copyright 2018 The Skycfg Authors.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# SPDX-License-Identifier: Apache-2.0

v2 = proto.package("envoy.api.v2")
corev2 = proto.package("envoy.api.v2.core")
endpointv2 = proto.package("envoy.api.v2.endpoint")
listenerv2 = proto.package("envoy.api.v2.listener")
routev3 = proto.package("envoy.config.route.v3")
accesslogv3 = proto.package("envoy.config.accesslog.v3")

hcmv3 = proto.package("envoy.extensions.filters.network.http_connection_manager.v3")
fileAccessLoggers = proto.package("envoy.extensions.access_loggers.file.v3")

protobuf = proto.package("google.protobuf")


def address(addr, port):
    return corev2.Address(
        socket_address=corev2.SocketAddress(
            address=addr,
            port_value=port,
        )
    )


def virtual_host(name):
    return routev3.VirtualHost(
        name=name,
        domains=["*"],
        routes=[
            routev3.Route(
                match=routev3.RouteMatch(prefix="/"),
                route=routev3.RouteAction(cluster=name),
            ),
        ],
    )


def hcm(cluster_name):
    access_log_config = fileAccessLoggers.FileAccessLog(
        path="/dev/stdout",
    )

    return hcmv3.HttpConnectionManager(
        stat_prefix="ingress_http",
        codec_type=hcmv3.HttpConnectionManager.CodecType.AUTO,
        access_log=[
            accesslogv3.AccessLog(
                name="log",
                typed_config=protobuf.Any(
                    type_url="type.googleapis.com/" + proto.name(access_log_config),
                    value=proto.to_proto(access_log_config),
                ),
            ),
        ],
        route_config=routev3.RouteConfiguration(
            name="local_route",
            virtual_hosts=[
                virtual_host(cluster_name),
            ],
        ),
    )


def proxy_listener(address, cluster_name):
    http_connection_manager = hcm(cluster_name)
    listener = v2.Listener(
        name="listener_0",
        address=address,
        filter_chains=[
            listenerv2.FilterChain(
                filters=[
                    listenerv2.Filter(
                        name="envoy.filters.network.http_connection_manager",
                        typed_config=protobuf.Any(
                            type_url="type.googleapis.com/"
                            + proto.name(http_connection_manager),
                            value=proto.to_proto(http_connection_manager),
                        ),
                    ),
                ],
            )
        ],
    )
    return listener


def listeners(cluster_name):
    return [
        proxy_listener(address("0.0.0.0", 10000), cluster_name),
    ]


def clusters(cluster_name):
    cluster = v2.Cluster(
        name=cluster_name,
        connect_timeout=protobuf.Duration(
            seconds=1,
            nanos=0,
        ),
        health_checks=[
            corev2.HealthCheck(
                http_health_check=corev2.HealthCheck.HttpHealthCheck(
                    path="/",
                ),
                unhealthy_threshold=1,
                healthy_threshold=1,
                timeout=protobuf.Duration(seconds=1),
                interval=protobuf.Duration(seconds=1),
                no_traffic_interval=protobuf.Duration(seconds=5),
            ),
        ],
        type=v2.Cluster.DiscoveryType.STRICT_DNS,
        load_assignment=v2.ClusterLoadAssignment(
            cluster_name=cluster_name,
            endpoints=[
                endpointv2.LocalityLbEndpoints(
                    lb_endpoints=[
                        endpointv2.LbEndpoint(
                            endpoint=endpointv2.Endpoint(
                                address=address("host.docker.internal", 8000),
                            )
                        )
                    ],
                )
            ],
        ),
    )

    return [cluster]


def main(ctx):
    cluster_name = "a_service"
    resources = [] + listeners(cluster_name) + clusters(cluster_name)
    return resources
